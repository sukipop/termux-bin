#!/usr/bin/env zsh
script_name="${0##*/}"
cache_dir="${HOME}/.cache"
cache_file="${cache_dir}/weather.json"
config_dir="${HOME}/.config/weather"

# Default settings
units="metric"
location="current"
force_update=0
max_data_age=300

# Function to display help message
function print_usage {
    cat <<- EOF

Usage: ${script_name} [options]

Options:
  -h, --help      Display this help message.

EOF
}

# Function to print error message
function error {
    local message="${1:-Unknown error}"
    local color="${COLOR_RED:-#ff0000}"
    print -P "%B%F{$color}Error%f:%b $message" >&2
}

# Function to check for required packages
function pkg_check {
    local requirements=("jq" "bc")
    local missing_pkgs=()

    # Check for missing packages
    for pkg in "${requirements[@]}"; do
        if ! command -v "$pkg" &>/dev/null; then
            missing_pkgs+=("$pkg")
        fi
    done

    # Display error if missing packages
    if [[ "${#missing_pkgs[@]}" -gt 0 ]]; then
        error "Missing packages: ${missing_pkgs[*]}"
        return 1
    fi
}

# Function to parse arguments
function parse_arguments {
    while (( "$#" )); do case "$1" in
        -h|--help|--help-all|-help|help)
            print_usage
            exit 0
            ;;
        -m|--metric)
            units="metric"
            ;;
        -f|--fahrenheit)
            units="fahrenheit"
            ;;
        --force)
            force_update=1
            ;;
        -*)
            error "Unknown option: $1"
            print_usage
            exit 1
            ;;
        *)
            error "Unknown argument: $1"
            print_usage
            exit 1
            ;;
        esac
        shift
    done
}

# Function to get openweathermap token
function get_token {
    local token_file="${config_dir}/token"

    # Use 'OWM_TOKEN' if set
    if [[ -n "$OWM_TOKEN" ]]; then
        token="$OWM_TOKEN"
        return 0
    # Use 'token_file' if found
    elif [[ -f "$token_file" ]] && source "$token_file"; then
        if [[ -n "$token" ]]; then
            return 0
        fi
    fi

    # Ask user for owm token
    print "Please enter your OpenWeatherMap API key:"
    if read token && [[ -n "$token" ]]; then
        print "token=${token}" >> "$token_file"
        return 0
    fi

    error "Invalid input or no input received."
    exit 1
}

# Function to get location
function get_location {
    local loc_data
    if [[ "$location" == "current" ]]; then
        loc_data=$(termux-location -r last)
        if [[ "$?" -eq 0 && -n "$loc_data" ]]; then
            # Extract latitude and longitude
            lat=$(jq -r '.latitude' <<< "$loc_data")
            lon=$(jq -r '.longitude' <<< "$loc_data")
            # Shorten to 4 decimals
            lat=$(printf "%.4f" "$lat")
            lon=$(printf "%.4f" "$lon")
            if [[ -n "$lat" && -n "$lon" ]]; then
                return 0
            fi
        fi
    else
        error "City names not yet supported"
        return 1
    fi

    error "Failed to determine location"
    return 1
}

# Function to read weather data from cache
function read_cache {
    local date_age dt

    # Ensure 'cache_file' exists
    if [[ ! -f "$cache_file" ]]; then
        return 1
    fi

    weather_data=$(cat "$cache_file")

    # Check data age
    dt=$(jq -c '.current.dt' <<< "$weather_data")
    data_age=$(($(date -u +%s)-$dt))
    if [[ "$data_age" -gt "$max_data_age" ]]; then
        return 2
    fi

    # Check data location
    data_lat=$(jq -c '.lat' <<< "$weather_data")
    data_lon=$(jq -c '.lon' <<< "$weather_data")

    # Simple threshold check for latitude and longitude
    if [[ $(bc <<< "$lat - $data_lat > 0.01 || $lon - $data_lon > 0.01") -eq 1 ]]; then
        return 3
    fi
}


# Function to update weather data
function update_weather_data {
    local url="https://api.openweathermap.org/data/3.0/onecall"
    url="${url}?lat=${lat}&lon=${lon}&appid=${token}"
    url="${url}&units=${units:-metric}"

    # Send request
    curl -s -f "$url" -o "$cache_file"
    if [[ "$?" -ne 0 ]]; then
        error "Failed to retrieve weather data"
        return 1
    fi
}

function main {
    local token

    parse_arguments "$@"

    # Ensure dirs exists
    mkdir -p "$cache_dir" "$config_dir"

    # Define 'token'
    get_token
    if [[ -z "$token" ]]; then
        exit 1
    fi

    get_location || exit 1

    read_cache

    if [[ "$?" -ne 0 || "$force_update" -ne 0 ]]; then
        update_weather_data || exit 1
    fi

    print "$weather_data"
}

main "$@"
