#!/usr/bin/env zsh
script_name="$(basename $0)"

# Function to display help message
function print_usage {
    print "Usage: $script_name [options]"
    print
    print "Display a grid of terminal colors."
    print
    print "Options:"
    print "  -h, --help    Show this help message."
    print
}

# Function to print error message
function error {
    local message="${1:-Unknown error}"
    local color="${COLOR_RED:-#ff0000}"
    print -P "%F{$color}%BError%f:%b $message" >&2
}

# Parse arguments
while [[ "$#" -gt 0 ]]; do
    case "$1" in
        -h|--help|-help|help|h)
            print_usage
            exit 0
            ;;
        -*)
            error "Unknown option: $1"
            print_usage
            exit 1
            ;;
        *)
            error "Unknown argument: $1"
            print_usage
            exit 1
            ;;
    esac
    shift
done

# Print terminal colors
for i in {0..255}; do
    print -Pn "%K{$i}  %k${(l:3::0:)i} " ${${(M)$((i%6)):#3}:+$'\n'}
done
